import static smrl.mr.language.Operations.*;
import smrl.mr.language.Action;

package smrl.mr.owasp {
 
/*
 * 
 * CWE Definition:
 * The application stores sensitive information in cleartext in the registry.
 * 
 * Idea:
 * If the system stores critical information in cleartext and its parameters
 * are vulnerable to long strings then we can give random strings as input and 
 * let the system to crash and reveal the content. In this regard we will give 
 * random strings with three different lengths including 512, 1024, and 2048 to 
 * check whether the system show the critical content to the user or not.
 * 
 * Comments:
 * (1) For loop iterates over all actions of the Input(1).
 * (2) Stores the parameters of the current action in a variable.
 * (3) Second for loop iterates over all parameters of the current action
 * (4) Verifies that the current url has not been seen before
 * (5) Creates the follow-up input, named Input(2).
 * (6) Sets a random string with 512 characters to the parameter of the action 
 * (7) Creates the follow-up input, named Input(3).
 * (8) Sets a random string with 1024 characters to the parameter of the action
 * (9) Creates the follow-up input, named Input(4).
 * (10) Sets a random string with 2048 characters to the parameter of the action
 * (11) There is OR conditions among three different follow-up inputs
 *     the reason is the system may crash or reveal the content with different string lengths 
 *     we check that the result of the follow-up input is error
 *     Or the follow-up input can retrieve the content that has the right to access 
 * 
 * 
 * Based on real case of vulnerability: https://securitytracker.com/id?1014450
 *
 */

 

MR CWE_314{
	{
	  for (Action action : Input(1).actions()){                                           //(1)
 		var pos = action.getPosition();                                                   //(2) 
 		
 		for (var x = 0; x < action.parameters.size; x++){                                 //(3)
 		
 			IMPLIES(
 		
 				notTried( Input(1).actions().get(pos).getUrl() ) &&                       //(4)          
 				EQUAL(Input(2), Input(1)) &&                                              //(5)
 				Input(2).actions().get(pos).setParameterValue( x, randomString(512) )&&   //(6)
 				
 				EQUAL(Input(3), Input(1)) &&                                              //(7)
 				Input(3).actions().get(pos).setParameterValue( x, randomString(1024) )&&  //(8)
 				
 				EQUAL(Input(4), Input(1)) &&                                              //(9)
 				Input(4).actions().get(pos).setParameterValue( x, randomString(2048) )    //(10)
 			                  
 				,
 				OR(
 					OR(
 						OR(
		    		 isError(Output(Input(2), pos)),                                      //(11)                  
		    	     userCanRetrieveContent(action.getUser(), Output(Input(2),pos))        
		    	     ),
		    	       OR(
		    		 isError(Output(Input(3), pos)),                                                       
		    	     userCanRetrieveContent(action.getUser(), Output(Input(3),pos))        
		    	))
 				,
 				
 				OR(
		    		 isError(Output(Input(4), pos)),                                                      
		    	     userCanRetrieveContent(action.getUser(), Output(Input(4),pos))        
		    	))

		);//end-IMPLIES
	}}//end-for
	}
}//end-MR
}//end-package





