import static smrl.mr.language.Operations.*

package smrl.mr.owasp {

/**
 * CWE Definition:
 * The software does not properly manage a user within its environment.
 * 
 * Idea:
 * A URL that cannot be reached by a user while navigating the user interface should not 
 * be available to that same user even when she directly requests the URL to the server. 
 * 
 * Comments: 
 * (1) The for loop iterates over all the actions of an input sequence.
 * (2) Checks whether the user in User() is not a supervisor of the user performing the y-th action.
 * (3) Verifies that the y-th action is performed after a login.
 * (4) Verifies that the follow-up user cannot retrieve the URL of the action through the GUI (based on the data collected by the crawler).
 * (5) Defines a follow-up input that performs the login as the follow-up user
 * (6) The system checks if the y-th action from the source input leads to an error page 
 *     Or the output generated by the action containing the URL indicated above, lead to two different outputs in the two cases.
 *   	
 */ 
  MR CWE_286_OTG_AUTHZ_002c {
  	{
    	for(var y = Input(1).actions().size()-1; ( y > 0 ); y--){                          //(1)
  			IMPLIES(
  				//1st par of IMPLIES including 4 clauses
  				(!isSupervisorOf(User(), Input(1).actions().get(y).user)) &&               //(2)
  				afterLogin(Input(1).actions().get(y)) &&                                   //(3)
  				cannotReachThroughGUI(User(), Input(1).actions().get(y).getUrl()) &&       //(4)
  				EQUAL(Input(2), Input(LoginAction(User()), Input(1).actions().get(y)) )    //(5)
  				,
  				//2nd par of IMPLIES
  				OR(                                                                        //(6)
  					isError(Output(Input(1), y)),
  					different(
	  					Output(Input(1), y),
	  					Output(Input(2), 1)
  					)
				)
  			); //end-IMPLIES
		} //end-for
    }
  } //end-MR
} //end-package