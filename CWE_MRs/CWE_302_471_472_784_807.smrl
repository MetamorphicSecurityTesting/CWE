/**
 * Authors:
 * Nazanin Bayati Chaleshtari, n.bayati@uottawa.ca (First implementation and maintenance)
 * Fabrizio Pastore, fabrizio.pastore@uni.lu (Validation)
 */
 
import static smrl.mr.language.Operations.*;
import smrl.mr.language.Action;
import java.util.Map.Entry;
import org.openqa.selenium.Cookie;
import smrl.mr.language.CookieSession;

package smrl.mr.owasp {
 
/*
 * CWE definition:
 * 
 * CWE_302
 * The authentication scheme or implementation uses key data elements that are assumed to be immutable, 
 * but can be controlled or modified by the attacker.
 * 
 * CWE_471:
 * The software does not properly protect an assumed-immutable element from being modified by an attacker.
 * 
 * CWE_472(child of CWE_471):
 * The web application does not sufficiently verify inputs that are assumed to be immutable but are actually 
 * externally controllable, such as hidden form fields.
 * 
 * CWE_784:
 * The application uses a protection mechanism that relies on the existence or values of a cookie, but it 
 * does not properly ensure that the cookie is valid for the associated user.
 * 
 * CWE_807:
 * The application uses a protection mechanism that relies on the existence or values of an input, but the 
 * input can be modified by an untrusted actor in a way that bypasses the protection mechanism.
 * 
 * Idea:
 * Focus on cookies as the key data:
 * Set the cookie path to a random cookie to check whether the system authenticates the user or not.
 * 
 * 
 * Comments:
 * (1) For loop iterates over all actions of the Input(1).
 * (2) Stores the parameters of the current action in a variable.
 * (3) Saves the cookie value of the Input(1) in a variable.
 * (4) Creates a follow-up input with different credentials.
 * (5) Saves the cookie value of the follow-up user in a variable.
 * (6) To speed up the process, verifies that the element URL has not been tried before. 
 * (7) Stores the session key values in a variable.
 * (8) For loop iterates over the session values.
 * (9) Assigns the type of the cookie to a variable.
 * (10) Filters all cookie types other than Boolean
 * (11) Makes sure the URL is not accessible without login.
 * (12) To avoid False positives, filters the actions with the same output for Input(1) and Input(2).
 * (13) Creates the follow-up input, named Input(3).
 * (14) Sets the session2 as the session value of the Input(3).
 * (15) Flip the value of the boolean cookie.
 * (16) The system should give a different output or should show an error.
 *  
 * 
 */

 MR CWE_302_471_472_784_807{ 
	{
   for (Action action: Input(1).actions()){                                                         //(1)
      var pos = action.getPosition();                                                               //(2)
      var session = Output( Input(1), pos).session as CookieSession;                                //(3)
      
      CREATE( Input(2), changeCredentials(Input(1), User()) )                                       //(4)
      var session2 = Output( Input(2), pos).session as CookieSession;                               //(5)
      var notTried = notTried( action.url, Input(1).actions().get(pos).getElementURL() )            //(6) 
      var mappings = session.keyValueMappings.entrySet;                                             //(7)
	  
	  for ( Entry<String,String> cookie : mappings){                                                //(8)
	  		var type = typeOf(cookie.value);                                                        //(9)
      
      IMPLIES( 
      		notTried																				
      		&& type == Boolean                                                                      //(10)
      		&& cannotReachThroughGUI( User(), action.url )                                          //(11)
      		&& different( Output( Input(1), pos) , Output(Input(2) , pos) )   					    //(12)
      		&& CREATE ( Input(3), Input(2) )                                                        //(13)
      		&& Input(3).actions.get(pos).setSession( session2 )                                     //(14)
      		                        
            && session.setCookie(      
            	new Cookie(cookie.key, String.valueOf( ! Boolean.valueOf(cookie.value) ))           //(15)
            ),
             OR(                                                                                    //(16)
             	different( Output( Input(1), pos) , Output(Input(3) , pos) )   ,                   
		 		isError( Output(Input(3) , pos)))
		);//end-IMPLIES
	  }//end-for
	}//end-for	
   }
  }//end-MR
 }//end-package
 
 
