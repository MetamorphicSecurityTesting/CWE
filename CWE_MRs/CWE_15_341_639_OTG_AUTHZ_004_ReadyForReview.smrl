import static smrl.mr.language.Operations.*;
import smrl.mr.language.Action;

package smrl.mr.owasp{

/**
 * CWE Definition:
 * One or more system settings or configuration elements can be externally controlled by a user.
 * 
 * Idea:
 * By changing the parameter values passed to URLs, a user should not be able to retrieve content
 * she cannot retrieve from GUI.
 * 
 * (1) The first loop iterates over all the actions of the input sequence. 
 * (2) The second iterates over all the parameters of the action.
 * (3) Stores the parameters of the current action in a variable.
 * (4) Defines the follow-up input. 
 * (5) Sets a parameter value to a random value.
 * (6) Checks if the content of the output generated by the login operation is either an error message 
 *     Or some content that can be retrieved from the GUI.
 */ 
	MR CWE_15_341_639_OTG_AUTHZ_004 {
		{
			for ( Action action : Input(1).actions() ){                                             //(1)
				for ( var par=0; par < action.getParameters().size(); par++ ){                      //(2)
					var pos = action.getPosition();                                                 //(3)
					IMPLIES( 
						EQUAL ( Input(2), Input(1) ) &&	                                            //(4)
					  	Input(2).actions().get(pos)		                                            
							.setParameterValue(par, parameterValueUsedByOtherUsers(action, par))    //(5)
							//TODO: optimization -> return only param_value of this action 
						, 
						OR( 							//2nd par of IMPLIES
							Output(Input(2),pos).isError()                                          //(6)
							, 
							userCanRetrieveContent( action.user, Output(Input(2),pos) ) 
						)
					);//end-IMPLIES
				}//end-for
			}//end-for
		}
	}//end-MR
}
