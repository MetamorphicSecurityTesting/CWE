import static smrl.mr.language.Operations.*;
import smrl.mr.language.Action;

package smrl.mr.owasp{

/**
 * CWE Definition:
 * CWE_20:
 * The product receives input or data, but it does not validate or incorrectly validates that the input has the properties that are required to process the data safely and correctly.
 * 
 * CWE_73:
 * The software allows user input to control or influence paths or file names that are used in filesystem operations.
 * 
 * CWE_99:
 * The software receives input from an upstream component, but it does not restrict or incorrectly restricts the input before it is used as an identifier for 
 * a resource that may be outside the intended sphere of control.
 * 
 * CWE_219:
 * The application stores sensitive data under the web document root with insufficient access control, which might make it accessible to untrusted parties.
 * 
 * CWE_220:
 * The application stores sensitive data under the FTP server root with insufficient access control, which might make it accessible to untrusted parties.
 * 
 * CWE_528;
 * The product generates a core dump file in a directory, archive, or other resource that is stored, transferred, or otherwise made accessible to unauthorized actors.
 * 
 * CWE_530:
 * A backup file is stored in a directory or archive that is made accessible to unauthorized actors.
 * 
 * CWE_642:
 * The software stores security-critical state information about its users, or the software itself, in a location that is accessible to unauthorized actors.
 * 
 * CWE_732:
 * The product specifies permissions for a security-critical resource in a way that allows that resource to be read or modified by unintended actors.
 * 
 * 
 * Idea:
 * A file path passed in a parameter should never enable a 
 * user to access data that is not provided by the user interface.
 *
 * (1) Iterates over the actions of the Input(1).
 * (2) The second for loop iterates over the parameters of the action.
 * (3) Stores the parameters of the current action in a variable.1
 * (4) Creates the follow-up input, named Input(2).
 * (5) Sets the value of a parameter to a random file path.
 * (6) Verifies that the system shows an error page
 *     Or the returned content is something that the user has the right to access.
 * 
 * 
 */ 
MR CWE_20_73_99_219_220_528_530_642_732_OTG_AUTHZ_001a {
 {
	for ( Action action : Input(1).actions() ){                                           //(1)
		for ( var par=0; par < action.getParameters().size(); par++ ){                    //(2)
			var pos = action.getPosition();                                               //(3)
			IMPLIES( 
				EQUAL( Input(2), Input(1) )	                                              //(4)
			  	  && Input(2).actions().get(pos).setParameterValue(par, RandomFilePath()) //(5)
			    ,
			    OR( 	                                                                  //(6)
			      isError( Output(Input(2),pos) ) 	
			      ,
			      userCanRetrieveContent(action.getUser(), Output(Input(2),pos)) )
			);//end-IMPLIES
		}//end-for
	}//end-for
 }
}//end-MR
}
