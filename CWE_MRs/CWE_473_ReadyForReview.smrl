import static smrl.mr.language.Operations.*
import smrl.mr.language.Action;

package smrl.mr.owasp {

   /**
   * CWE Definition:
   * A PHP application does not properly protect against the modification of variables from external sources, such as
   * query parameters or cookies. This can expose the application to numerous weaknesses that would not exist otherwise.
   * Do not allow your application to run with register_globals enabled.
   * 
   * Comments:
   * (1) define a variable with the attacker's session path value.
   * (2) For loop iterate over all actions of the Input(1)
   * (3) define a variable to store the position of the Input(1)'s action
   * (4) Retrieving the current URL that Input(1) has access to
   * (5) Creating a new URL by modifying the current URL (adding the attacker's session path to it).
   * (6) Making sure that Input(1) is taking an action that requires to be logged in.
   * (7) Creating the follow-up Input by copying Input(1) but with different credential values.
   * (8) Setting the new URL which is going to change the user's session
   * (9) If the system is well designed, then it will show an error page or
   * it may show the same session for the next action (it means it the attacker was not successful to change the session)
   *   	
   */ 
  MR OTG_473 {
  	{
  		
  		var global = "GLOBALS[user_session_path]=[EV!L]" //(1)
  		
  		//var global_username = "GLOBALS[username]=[username]"	
  		
	  	for(Action action: Input(1).actions()){ //(2)
	  		var pos = action.getPosition() //(3)
	  		var current_url = Input(1).actions().get(pos).getUrl()  //(4)
	  		var new_url = current_url + global; //(5)
	  			
  			IMPLIES(
  				isNotAccessibleWithoutLogin(Input(1).actions().get(pos))&&   //(6)
  				EQUAL(Input(2),changeCredentials( Input(1),User() ) )&&		 //(7)
  				Input(2).actions().get(pos).setUrl( new_url )                //(8)
  				,
  				//2nd par of IMPLIES
  				OR(
  					isError(Output(Input(2), pos)),    //(9)
  					!different(
	  					Output(Input(1), pos+1).getSession(),
	  					Output(Input(2), pos+1).getSession()
  					)
				)
	  				
	  			); //end-IMPLIES
  			} //end-for
		}
    
  } //end-MR
} //end-package