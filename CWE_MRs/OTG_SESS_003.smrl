/**
 * Authors:
 * Xuan Phu MAI, xuanphu.mai@uni.lu (First implementation)
 * Nazanin Bayati Chaleshtari, n.bayati@uottawa.ca (Maintenance)
 * Fabrizio Pastore, fabrizio.pastore@uni.lu (Validation)
 */

import static smrl.mr.language.Operations.*;
import smrl.mr.language.Action;
package smrl.mr.owasp{
	
	
/* 
 * Definition:
 * A login action performed by a user already authenticated should always trigger the generation of a new session ID.
 * 
 * Comments:
 * (1) The first loop iterates over the inputs to find a sign up action
 * (2) The second loop iterates over the actions that follow the sign up. The second loop is necessary to check that 
 *     a sign up action repeated at any point of the action sequence leads to a new session ID.
 * (3) Checks if the current action has been performed after a login.
 * (4) Defines a follow-up input with the sign up action being duplicated in a certain position.
 * (5) Checks if the session ID of the response page sent after the two successive login actions is different. 
 * 
 */ 
	
	MR OTG_SESS_003 {
		{
			for( Action signup : Input(1).actions() ){                                  //(1)
				for ( var i=0;
						isSignup(signup) && i < Input (2).actions().size; i++ ) {       //(2)
					var f = Input (2).actions().get(i);
					var pos = f.getPosition();
					IMPLIES (
							afterLogin ( f ) &&                                         //(3)
							CREATE (Input (3), addAction ( Input(2), pos+1, signup ))   //(4)
							,
							different(                                                  //(5)
									Output(Input(3), pos).getSession(),
									Output(Input(3), pos+1).getSession())
							);//end-IMPLIES
				}//end-for
			}//end-for
		}}//end-MR
}