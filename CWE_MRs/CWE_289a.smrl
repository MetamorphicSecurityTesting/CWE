/**
 * Authors:
 * Nazanin Bayati Chaleshtari, n.bayati@uottawa.ca (First implementation and maintenance)
 * Fabrizio Pastore, fabrizio.pastore@uni.lu (Validation)
 */

import static smrl.mr.language.Operations.*;
import smrl.mr.language.Action;

package smrl.mr.owasp {
 
/*
 *  
 * CWE Definition:
 * The software performs authentication based on the name of a resource being accessed,
 * or the name of the actor performing the access, but it does not properly check all possible names for that resource or actor.
 * 
 * Idea:
 * Protection mechanism that restricts URL access can be bypassed using URL with Hashed separators. 
 * 
 * comments:
 * (1) For loop iterates over all actions of the Input(1).
 * (2) Stores the parameters of the current action in a variable.
 * (3) Stores the URL of the current action in a variable.
 * (4) Considers users that only have lower or similar access level to Input(1).
 * (5) Filters the dynamic content.
 * (6) Ensure the follow-up user does not have access to the URL.
 * (7) Creates the follow-up Input by copying Input(1) with different credentials, named Input(2).
 * (8) Sets the encoded URL for input(2) to check that the URL action which is not accessible to the Input(2) would be accessible if it is encoded or not.
 * (9) The system should show a content that the user has right to access
 *     Or an error page or different outputs for two inputs. 
 * 
 */

MR CWE_289a {
	{
   for (Action action: Input(1).actions()){                                           //(1)
      var pos = action.getPosition();                                                 //(2)
      var url = action.getUrl();                                                      //(3)
      
      IMPLIES(                        
               (!isSupervisorOf(User(), action.user)) &&                              //(4) 
               userCanRetrieveContent(action.getUser(),Output(Input(1) , pos)) &&     //(5)
               cannotReachThroughGUI(User(),action.url) &&                            //(6) 
               CREATE(Input(2), changeCredentials(Input(1), User())) &&                //(7)
               Input(2).actions.get(action.position).setUrl(EncodeUrl(url))           //(8)
               
               
             ,OR(userCanRetrieveContent(action.getUser(),Output(Input(2) , pos))      //(9)
             	,
              OR(    different( Output( Input(1), pos) , Output(Input(2) , pos) ),   
           			  isError( Output(Input(2) , pos)))
		));//end-IMPLIES
	}//end-for
	}
}//end-MR
}//end-package
