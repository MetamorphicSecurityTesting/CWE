import static smrl.mr.language.Operations.*;
import smrl.mr.language.Action;

package smrl.mr.owasp {
 
/*
 * OK
 * 
 * CWE Definition:
 *  The software performs authentication based on the name of a resource being accessed,
 *  or the name of the actor performing the access, but it does not properly check all possible names for that resource or actor.
 * 
 * Protection mechanism that restricts URL access can be bypassed using URL encoding. 
 * 
 * comments:
 * (1) For loop iterates over all actions of the Input(1).
 * (2) Stores the parameters of the current action in a variable.
 * (3) Stores the URL of the current action in a variable.
 * (4) It considers users that only have lower or similar access level to Input(1).
 * (5) It makes sure that the follow-up user does not have access to the url.
 * (6) Creates the follow-up Input by copying Input(1) with different credentials, named Input(2).
 * (7) It encode the URL for input(2) to check that the url action which is not accessible to the Input(2) would be accessible if it is encoded or not.
 * (8) The system should show an error page or different outputs for two inputs. 
 */

MR CWE_289a{
	{
   for (Action action: Input(1).actions()){ //(1)
      var pos = action.getPosition();       //(2)
      var url = action.getUrl();            //(3)
      IMPLIES(             
                   
               (!isSupervisorOf(User(), action.user)) && //(4) 
                cannotReachThroughGUI(User(),action.url) && //(5) Returns true if ‘User’ can access the URLs of ‘action.user’.
               EQUAL(Input(2), changeCredentials(Input(1), User())) &&       //(6)
               Input(2).actions.get(action.position).setUrl(EncodeUrl(url)) //(7)
               
               
             , OR(    different( Output( Input(1), pos) , Output(Input(2) , pos) ), //(8)
           			  isError( Output(Input(2) , pos))
		));//end-IMPLIES
	}//end-for
	}
}//end-MR
}//end-package





