import static smrl.mr.language.Operations.*;
import smrl.mr.language.Action;
import smrl.mr.crawljax.WebProcessor

package smrl.mr.owasp{

/**
 * CWE Definition:
 * The web application does not, or can not, sufficiently verify whether a well-formed, 
 * valid, consistent request was intentionally provided by the user who submitted the request.
 * 
 * Idea:
 * A user shall not be able to successfully submit a form without retrieving it first.
 * Otherwise, someone else can create a crafted page with a submit form button.
 * 
 * Comments:
 * (1) It avoids clicking on OK; because dialogs are normally ignored by our framework by clicking on OK.
 * (2) For loop iterates over all actions of the Input(1)
 * (3) Stores the parameters of the current action in a variable
 * (4) Iterates over the actions that contain a form input.
 * (5) Checks if the action submits the form; i.e. the action contains clicking on submit button.
 * (6) Verifies that the URL has not been tried before.
 * (7) Makes sure the action is performed after the login.
 * (8) Creates Input(2) as the follow-up input from the user that takes the login action and
 * (9) It sets the URL of the file (to retrieve the file before uploading it), and the action.
 * (10) The system should show a different output.
 * 
 */
  
MR CWE_352 {
	{
	
	keepDialogsOpen = true;                                                                          //(1)

 	for (Action action : Input(1).actions()){                                                        //(2)
 	
 		var pos = action.getPosition();                                                              //(3) 
 		
 		for (var x = 0; action.containFormInput() && x < action.formInputs.size; x++){               //(4)		
 			IMPLIES(
 					action.isClickOnButton &&                                                        //(5)
 					notTried( x+action.url, Input(1).actions().get(pos).getElementURL() ) &&         //(6)        
 					afterLogin( action )    &&													     //(7)
 					EQUAL( Input(2), Input( LoginAction(action.user)   ,                             //(8)
 					RequestUrlAction("file://"+Output(Input(1),pos-1).file.absolutePath),action ) )  //(9)              
 				,
					different ( Output(Input(1),pos),  Output(Input(2),3) )                          //(10)
 				
 			);
 		}
 	}
 	
  } }
 }