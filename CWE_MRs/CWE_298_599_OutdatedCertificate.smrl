import static smrl.mr.language.Operations.*;
import smrl.mr.language.Action;
import sun.security.util.Password

package smrl.mr.owasp {
 
/**
 * 
 * CWE Definition:
 * CWE_298:
 * A certificate expiration is not validated or is incorrectly validated, so trust may be assigned to 
 * certificates that have been abandoned due to age.
 * 
 * CWE_599:
 * The software uses OpenSSL and trusts or uses a certificate without using the SSL_get_verify_result() 
 * function to ensure that the certificate satisfies all necessary security requirements.
 * 
 * Idea:
 * we can connect to a webserver after we have a certificate we can simply check if we can connect when we have an out dated 
 * certificate. Therefore, after login we will check if we can perform the action or not. 
 * 
 * Comments:
 * (1) For loop iterates over all actions of the Input(1)
 * (2) Stores the parameters of the current action in a variable
 * (3) It checks if the current action of the Input(1) is the login.
 * (4) Verifies that the current URL has not been seen before, to speed up the process.
 * (5) Filters the webpages with dynamic content.
 * (6) Creates the follow-up input.
 * (7) Sets an out dated certificate for the user.
 * (8) The system should show the error 
 *     Or the follow-up input should retrieve the content that has right to access
 *     Or the result should be different.
 * 
 * 
 */
 
MR CWE_298_599 {  
 { 
    for ( Action action : Input(1).actions() ) {                                        //(1)
	  	var pos = action.getPosition();                                                 //(2)
	  
	   IMPLIES(
	   		   isLogin(action) &&                                                       //(3)
	           notTried( action.url, Input(1).actions().get(pos).getElementURL() )  &&  //(4)
	           userCanRetrieveContent(action.user, Output(Input(1), pos))&&             //(5)
	           EQUAL ( Input(2), Input(1) ) &&	                                        //(6)
	           Input(2).setUseOutdatedCertificate(true)                                 //(7)
		    ,  
		    OR(                                                                         //(8)
		    	isError(Output(Input(2), pos))    
		    	,
		    OR	(userCanRetrieveContent(action.user, Output(Input(2), pos)) ,
		    		different ( Output(Input(1),pos),  Output(Input(2),pos))
		    		)
		    	)   	
		);//end-IMPLIES
	}//end-for
 } 
}//end-MR
}//end-package





