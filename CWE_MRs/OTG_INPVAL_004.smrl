/**
 * Authors:
 * Xuan Phu MAI, xuanphu.mai@uni.lu (First implementation)
 * Nazanin Bayati Chaleshtari, n.bayati@uottawa.ca (Maintenance)
 * Fabrizio Pastore, fabrizio.pastore@uni.lu (Validation)
 */

import static smrl.mr.language.Operations.*;
import smrl.mr.language.Action;
package smrl.mr.owasp{
	
/*
 * Definition:
 * Duplicating a parameter value should not lead to a different behaviour in the system.
 * 
 * Comments:
 * (1) The first loop iterates over all the actions of an input sequence.
 * (2) The second loop iterates over all the parameters.
 * (3) Stores the position of the current action in a variable.
 * (4) Defines a follow-up input.
 * (5) Duplicates one parameter.
 * (6) verifies that the output generated by the modified action is the same in the two cases.
 * 
 */
	MR OTG_INPVAL_004 {
		{
			for ( Action action : Input(1).actions() ){                              //(1)
				for (var par=0; par < action.getParameters().size(); par++ ){        //(2)
					var pos = action.getPosition();                                  //(3)
					IMPLIES (
							( CREATE ( Input(2), Input(1) ) &&                       //(4)
									Input(2).actions().get(pos).addParameter(        //(5)
											action.getParameterName(par),
											action.getParameterValue(par) ))
							,
							equal ( Output(Input(1) ) , Output( Input(2) ))          //(6)
				);//end-IMPLIES
	}//end-for
   }//end-for
  } 
 }//end-MR
}//end-package