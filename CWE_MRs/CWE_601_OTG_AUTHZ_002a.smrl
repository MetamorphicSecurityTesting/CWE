import static smrl.mr.language.Operations.*
import smrl.mr.language.Action;

package smrl.mr.owasp {

/**
 * 
 * CWE Definition:
 * A web application accepts a user-controlled input that specifies a link to an external site, 
 * and uses that link in a Redirect. This simplifies phishing attacks.
 * 
 * Idea:
 * If a redirecting URL cannot be reached by a user while navigating the user interface,
 * the same URL, if directly requested to the server, should not 
 * enable the same user to access a page where the click on one of its elements 
 * (e.g., a warning message) enables the user to access the content of the URL.
 * 
 * Comments:
 * (1) The for loop iterates over all actions of the Input(1).
 * (2) Stores the parameters of the current action in a variable.
 * (3) Checks whether the user in User() is not a supervisor of the user performing the current action.
 * (4) Verifies that the user cannot retrieve the URL of the action through the GUI (based on the data collected by the crawler).
 * (5) Defines a follow-up input that matches the source input except that the credentials of User() are used in this case.
 * (6)  It is made of three clauses.
 *      The 1st clause verifies that the original URL does not perform any redirect.
 *      The 2nd clause verifies that the original URL does not perform any redirect.
 *      The 3rd clause verifies that the follow up input does not lead to the same redirect from the original input.
 *  
 * 
 */ 
  MR CWE_601_OTG_AUTHZ_002a {
    {
    	for ( Action action : Input(1).actions() ){                         //(1)
    		var pos = action.getPosition();                                 //(2)
	  		IMPLIES(
	  			(!isSupervisorOf(User(), action.user)) &&					//(3)
			  	cannotReachThroughGUI(User(), action.url) &&				//(4)
			    EQUAL( Input(2), changeCredentials( Input(1), User() ) )	//(5)
				,
				( Output(Input(1), pos).redirectURL()===null ||				//(6)
					Output(Input(2), pos).redirectURL()===null ) ||			
					NOT(													
						EQUAL ( 
							Output(Input(2), pos).redirectURL(), 
							Output(Input(1), pos).redirectURL()
						)
					)
			);	//end-IMPLIES
		} //end-for
    }
  } //end-MR
} //end-package