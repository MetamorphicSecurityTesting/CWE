/**
 * Authors:
 * Nazanin Bayati Chaleshtari, n.bayati@uottawa.ca (First implementation and maintenance)
 * Fabrizio Pastore, fabrizio.pastore@uni.lu (Validation)
 */

import static smrl.mr.language.Operations.*;
import smrl.mr.language.Action;

package smrl.mr.owasp {
 
/*
 * 
 * CWE Definition:
 * The application stores sensitive information in cleartext in the registry.
 * 
 * Idea:
 * If the system stores critical information in cleartext and its parameters
 * are vulnerable to long strings then we can give random strings as input and 
 * let the system to crash and reveal the content. In this regard we will give 
 * random strings with three different lengths including 512, 1024, and 2048 to 
 * check whether the system show the critical content to the user or not.
 * 
 * Comments:
 * (1) For loop iterates over all actions of the Input(1).
 * (2) Stores the parameters of the current action in a variable.
 * (3) Defines random string with three different lengths.
 * (4) Second for loop iterates over all parameters of the current action.
 * (5) Makes sure the current action does not have any alerts.
 * (6) Filters the dynamic content.
 * (7) Verifies that the current URL has not been seen before
 * (8) Creates the follow-up input, named Input(2).
 * (9) Sets a random string with 512 characters to the parameter of the action 
 * (10) Creates the follow-up input, named Input(3).
 * (11) Sets a random string with 1024 characters to the parameter of the action
 * (12) Creates the follow-up input, named Input(4).
 * (13) Sets a random string with 2048 characters to the parameter of the action
 * (14) There is OR conditions among three different follow-up inputs
 *     the reason is the system may crash or reveal the content with different string lengths 
 *     we check that the result of the follow-up input is error
 *     Or the follow-up input can retrieve the content that has the right to access
 *     Or either the attack was not performed
 * 
 * 
 * Based on real case of vulnerability: https://securitytracker.com/id?1014450
 *
 * 
 */

 
MR CWE_314{
	{
	  for (Action action : Input(1).actions()){                                           //(1)
 		var pos = action.getPosition();                                                   //(2) 
 		var str512 = action.randomString(512);                                            //(3)
 		var str1024 = action.randomString(1024);
 		var str2048 = action.randomString(2048);
 		for (var x = 0; x < action.parameters.size; x++){                                 //(4)
 		
 			IMPLIES(
 				
 				! Output(Input(1), pos).hasAlert  &&                                      //(5)
 		 		userCanRetrieveContent(action.getUser(), Output(Input(1),pos))   &&       //(6)
 				notTried( Input(1).actions().get(pos).getUrl() ) &&                       //(7)          
 				
 				CREATE(Input(2), Input(1)) &&                                              //(8)
 				Input(2).actions().get(pos).setParameterValue( x, str512 )&&              //(9)
 				
 				CREATE(Input(3), Input(1)) &&                                              //(10)
 				Input(3).actions().get(pos).setParameterValue( x, str1024 )&&             //(11)
 				
 				CREATE(Input(4), Input(1)) &&                                              //(12)
 				Input(4).actions().get(pos).setParameterValue( x, str2048 )               //(13)
 			                  
 				,
 		OR(	OR(                                                                           //(14)
 					OR(
 						OR(
		    		 isError(Output(Input(2), pos)),                                                       
		    	     userCanRetrieveContent(action.getUser(), Output(Input(2),pos))        
		    	     ),	OR(                                                                            
		    		 //either the attack was not performed
		    		 Output(Input(2), pos).emptyFile,
		    		     
		    	    ! Output(Input(2), pos).hasAlert   	)
		    	)
		    	
		    	,
		    	OR(
 						OR(
		    		 isError(Output(Input(3), pos)),                                                      
		    	     userCanRetrieveContent(action.getUser(), Output(Input(3),pos))        
		    	     ),	OR(                                                                            
		    		 //either the attack was not performed
		    		 Output(Input(3), pos).emptyFile,
		    		     
		    	    ! Output(Input(3), pos).hasAlert        
		    	)
		    	)),
		    	OR(
 						OR(
		    		 isError(Output(Input(4), pos)),                                                   
		    	     userCanRetrieveContent(action.getUser(), Output(Input(4),pos))        
		    	     ),	OR(                                                                          
		    		 //either the attack was not performed
		    		 Output(Input(4), pos).emptyFile,
		    		     
		    	    ! Output(Input(4), pos).hasAlert        
		    	)
		    	))
		);//end-IMPLIES
	}}//end-for
	}
}//end-MR
}//end-package





